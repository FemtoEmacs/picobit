#lang racket

(require "utilities.rkt"
         "ast.rkt"
         "primitives.rkt"
         "reader.rkt"
         "parser.rkt"
         "front-end.rkt"
         "ir.rkt"
         "comp.rkt"
         "back-end.rkt"
         "encoding.rkt"
         "analysis.rkt"
         "scheduling.rkt"
         "tree-shaker.rkt")
(require racket/pretty)

;-----------------------------------------------------------------------------

(define (compile filename)
  (let* ([hex-filename (path-replace-suffix filename ".hex")]
         [forms        (read-file  filename)]
         [global-env   (make-global-env)]
         [node         (parse-top-list forms global-env)])

    (mark-needed-global-vars! global-env node)

    (let ([node (extract-parts-top node global-env parse)])
      (adjust-unmutable-references! node)
      (when (show-parsed?)
        (pretty-print (node->expr node)))
      (let* ([ctx  (comp-none node (make-init-context))]
             [code (context-code ctx)]
             [bbs  (code->vector code)])
        (resolve-toplevel-labels! bbs)
        (let* ([bbs  (reorder! (tree-shake! bbs))]
               [prog (linearize bbs)])
          (when (show-asm?)
            (pretty-print prog))
          ;; r5rs's with-output-to-file (in asm.rkt) can't overwrite. bleh
          (when (file-exists? hex-filename)
            (delete-file hex-filename))
          (let ([size (assemble prog hex-filename)])
            (when (show-size?)
              (printf "~a bytes\n" size))))))))

(command-line
 #:once-each
 [("--size")
  "Display the size of the generated bytecode."
  (show-size?  #t)]
 [("--parse")
  "Display post-parsing representation of the program."
  (show-parsed? #t)]
 [("-S" "--asm")
  "Display generated bytecode pre-assembly."
  (show-asm? #t)]
 [("--stats")
  "Display statistics about generated instructions."
  (stats? #t)]
 #:args (filename)
 (void (compile filename)))
